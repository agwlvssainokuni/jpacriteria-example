plugins {
    id 'java-library'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.4.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

sourceSets {
    entity
    entitymodel {
        java {
            srcDir file("build/generated/sources/annotationProcessor/java/entity")
        }
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.1"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework:spring-orm'
    implementation 'jakarta.persistence:jakarta.persistence-api'
    implementation 'org.hibernate.orm:hibernate-core'
    runtimeOnly 'com.h2database:h2'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    entityCompileOnly 'jakarta.persistence:jakarta.persistence-api'
    entityCompileOnly 'org.projectlombok:lombok:1.18.34'
    entityAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
    entityAnnotationProcessor 'org.hibernate.orm:hibernate-jpamodelgen'

    entitymodelCompileOnly 'jakarta.persistence:jakarta.persistence-api'
    entitymodelCompileOnly sourceSets.entity.output

    implementation sourceSets.entity.output

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
